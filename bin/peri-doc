#!/usr/bin/perl

use 5.010;
use strict;
use warnings;

# VERSION

eval { require Perinci::CmdLine };
if ($@) {
    die "This script requires Perinci::CmdLine, please install it first.\n";
}

our %SPEC;

$SPEC{generate_doc} = {
    v => 1.1,
    args => {
        url => {
            summary => 'URL',
            req => 1,
            pos => 0,
            schema => 'str*',
        },
        format => {
            summary => 'Format',
            schema => 'str',
        },
    },
};
sub generate_doc {
    require File::Which;

    my %args = @_;
    # XXX schema
    my $url = $args{url} or return [400, "Please specify url"];

    my $format = $args{format} // "man";
    $format = "text" unless
        File::Which::which("pod2man") && File::Which::which("man");

    # XXX check if url is a module or function or ...

    my ($doc, $res);
    if ($format eq 'man') {
        require Perinci::To::POD;
        $doc = Perinci::To::POD->new(url=>$url);
        $res = $doc->generate_doc;
        [200, "OK", $res, {
            "cmdline.page_result"=>1,
            "cmdline.pager"=>"pod2man | man -l -"}];
    } else {
        require Perinci::To::Text;
        $doc = Perinci::To::Text->new(url=>$url);
        $res = $doc->generate_doc;
        [200, "OK", $res, {"cmdline.page_result"=>1}];
    }

}

my $cmd = Perinci::CmdLine->new(
    log_any_app => 0, # speed up start-up
    url => '/main/generate_doc',
);
delete $cmd->common_opts->{format};
delete $cmd->common_opts->{format_options};
$cmd->run;

# ABSTRACT: Display documentation of Riap code entities
# PODNAME: peri-doc

=head1 SYNOPSIS

From command-line:

 % peri-doc /Some/Module/
 % peri-doc --help


=head1 DESCRIPTION

=cut
